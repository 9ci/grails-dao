buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "gradle.plugin.com.energizedwork.webdriver-binaries:webdriver-binaries-gradle-plugin:1.1"
        classpath "gradle.plugin.com.energizedwork:idea-gradle-plugins:1.4"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
    }
}

apply plugin:"idea"
apply plugin:"com.energizedwork.webdriver-binaries"
apply plugin:"com.energizedwork.idea-project-components"
apply plugin: 'codenarc'

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url = "https://dl.bintray.com/9ci/grails-plugins"}
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:events"

    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.5.Final"
    compile "org.hibernate:hibernate-ehcache"

    compileOnly "org.grails.plugins:views-json:1.2.5"
    compileOnly "org.grails.plugins:views-json-templates:1.2.5"

    runtime "com.h2database:h2"
    runtime "org.apache.tomcat:tomcat-jdbc"

    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails:grails-web-testing-support"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-chrome-driver:3.6.0"
    testRuntime "org.seleniumhq.selenium:selenium-firefox-driver:3.6.0"
    testRuntime "org.seleniumhq.selenium:selenium-remote-driver:3.6.0"
    testRuntime "org.seleniumhq.selenium:selenium-api:3.6.0"

    //-----
    compileOnly "org.springframework:spring-test"
    //this is for GrailsParameterMapRowMapper that uses the MockHttpServletRequest
    compileOnly "org.springframework.data:spring-data-commons" //:1.12.10.RELEASE

    compile "org.codehaus.gpars:gpars:1.2.0"

    //used for bundled Test traits
    compileOnly "org.grails:grails-gorm-testing-support"
    compileOnly "org.grails:grails-web-testing-support"

    codenarc 'org.codenarc:CodeNarc:0.27.0'
}

//bootRepackage and maybe addResources are needed for the test-projects grails.plugin.compile depndency to work
bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
}
bootRepackage.enabled = false

codenarc {
    toolVersion = '0.27.0'
    config = project.resources.text.fromString("${commonBuild}/codenarc/rules.groovy".toURL().text)
    reportFormat = 'html'
    //ignoreFailures = true

    maxPriority1Violations = 0
    maxPriority2Violations = 4
    maxPriority3Violations = 4
}

codenarcMain {
    exclude '**/GormScalars.groovy'
}

codenarcTest {
    ignoreFailures = true
}

//this makes the groovy docs much more readable by shorting packages and providing links
groovydoc {
    excludes = ['**/*GrailsPlugin.groovy','**/Application.groovy' ]
    link('http://download.oracle.com/javase/8/docs/api/', 'java.', 'org.xml', 'javax.', 'org.xml.')
    link("https://docs.spring.io/spring/docs/4.2.x/javadoc-api/", 'org.springframework')
    link('http://groovy.codehaus.org/api/',  'groovy.', 'org.codehaus.groovy.')
    link('https://docs.grails.org/3.3.x/api',  'grails.', 'org.grails.')
    link('https://testing.grails.org/latest/api',  'grails.testing.', 'org.grails.testing.')
    link('http://gorm.grails.org/latest/hibernate/api/',  'grails.gorm', 'grails.orm', 'org.grails.datastore', 'org.grails.orm')
}

